# Maintainer (MSYS2): Michael Hansen <zrax0111@gmail.com>

_variant=-${KF6_VARIANT:-shared}
source "$(dirname ${BASH_SOURCE[0]})"/../mingw-w64-PKGBUILD-common/kde-frameworks
_realname=kate
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=24.02.2
pkgrel=1
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'archlinux: kate'
  "cpe: cpe:/a:kde:kate"
)
msys2_repository_url="https://invent.kde.org/utilities/kate/"
url="https://apps.kde.org/kate/"
license=('GPL' 'LGPL' 'FDL')
pkgdesc="Advanced Text Editor (mingw-w64)"
groups=("${MINGW_PACKAGE_PREFIX}-kde-applications"
        "${MINGW_PACKAGE_PREFIX}-kde-utilities")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-extra-cmake-modules"
             "${MINGW_PACKAGE_PREFIX}-icoutils"
             "${MINGW_PACKAGE_PREFIX}-kdoctools${_namesuff}"
             "${MINGW_PACKAGE_PREFIX}-ninja")
depends=("${MINGW_PACKAGE_PREFIX}-knewstuff${_namesuff}"
         "${MINGW_PACKAGE_PREFIX}-ktexteditor${_namesuff}"
         "${MINGW_PACKAGE_PREFIX}-threadweaver${_namesuff}"
         "${MINGW_PACKAGE_PREFIX}-kitemmodels${_namesuff}"
         "${MINGW_PACKAGE_PREFIX}-hicolor-icon-theme")
optdepends=("${MINGW_PACKAGE_PREFIX}-breeze-icons${_namesuff}: application icon theme"
            "${MINGW_PACKAGE_PREFIX}-kinit${_namesuff}: file system browser plugin")
source=(https://download.kde.org/stable/release-service/${pkgver}/src/${_realname}-${pkgver}.tar.xz{,.sig}
        "kate-24.02.2.diff")
sha256sums=('1ddacb0939831a43e2b9c1abd8d7e5afefe76789842333263655370e99f5434d'
            'SKIP' 'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

apply_patch_with_msg() {
  for _fname in "$@"
  do
    msg2 "Applying ${_fname}"
    patch -Nbp1 -i "${srcdir}"/${_fname}
  done
}

prepare() {
  mkdir -p build-${MSYSTEM}${_variant}

  cd ${_realname}-${pkgver}
  apply_patch_with_msg \
    kate-24.02.2.diff
}

build() {
  local -a extra_config
  cd build-${MSYSTEM}${_variant}
  if [ "${_variant}" = "-static" ]; then
    extra_config+=( -DBUILD_SHARED_LIBS=NO )
  fi

  _kde_build_env
  ${MINGW_PREFIX}/bin/cmake.exe \
    -DCMAKE_BUILD_TYPE=Release \
    "${_KDE_INSTALL_DIRS[@]}" \
    -DBUILD_QCH=OFF \
    -DBUILD_TESTING=OFF \
    -DECM_DIR=${MINGW_PREFIX}/share/ECM \
    "${extra_config[@]}" \
    -G'Ninja' \
    ../${_realname}-${pkgver}

  ${MINGW_PREFIX}/bin/cmake.exe --build ./
}

package() {
  cd build-${MSYSTEM}${_variant}
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake.exe --install ./
}
